# ctyun_ebm (Resource)
**详细说明请见文档：https://www.ctyun.cn/document/10027724**



## 样例

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-for-ebm"
  cidr        = "192.168.0.0/16"
  description = "terraform测试使用"
  enable_ipv6 = true
}

resource "ctyun_subnet" "subnet_test" {
  vpc_id = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-for-ebm"
  cidr        = "192.168.1.0/24"
  description = "terraform测试使用"
  dns         = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
  enable_ipv6 = true
  type = "common"
}

resource "ctyun_security_group" "security_group_test" {
  vpc_id = ctyun_vpc.vpc_test.id
  name        = "tf-sg-for-ebm"
  description = "terraform测试使用"
}

resource "ctyun_security_group" "security_group_test2" {
  vpc_id = ctyun_vpc.vpc_test.id
  name        = "tf-sg-for-ebm2"
  description = "terraform测试使用"
}

locals {
  device_type1 = "physical.s5.2xlarge4"      // az1、有本地盘、弹性、不支持云硬盘
  device_type2 = "physical.s5.2xlarge1"      // az2、无本地盘、弹性、支持云硬盘
  az2 = "cn-huadong1-jsnj2A-public-ctcloud"
}

data "ctyun_ebm_device_raids" "system_raid" {
  device_type = local.device_type1
  volume_type = "system"
}

data "ctyun_ebm_device_raids" "data_raid" {
  device_type = local.device_type1
  volume_type = "data"
}

data "ctyun_ebm_device_images" "test" {
  device_type = local.device_type1
  os_type = "linux"
  image_type = "standard"
}

locals {
  system_raids = data.ctyun_ebm_device_raids.system_raid.raids
  system_raid_id = length(local.system_raids) > 0 ? local.system_raids[0].uuid : null

  data_raids = data.ctyun_ebm_device_raids.data_raid.raids
  data_raid_id = length(local.data_raids) > 0 ? local.data_raids[0].uuid : null
}


data "ctyun_ebm_device_images" "dependence" {
  device_type = local.device_type2
  az_name = local.az2
  os_type = "linux"
  image_type = "standard"
}

resource "ctyun_eip" "eip_test" {
  name                = "tf-eip-for-ebm"
  bandwidth           = 1
  cycle_type          = "on_demand"
  demand_billing_type = "upflowc"
}

variable "password" {
  type      = string
  sensitive = true
}

# 创建一台带从云硬盘启动，带弹性IP的弹性裸金属
resource "ctyun_ebm" "ebm_test" {
  az_name   = local.az2
  instance_name = "tf-ebm-for-ebm"
  hostname = "tf-ebm-for-ebm"
  password = var.password
  eip_id = ctyun_eip.eip_test.id
  cycle_type = "on_demand"
  device_type = local.device_type2
  image_uuid = data.ctyun_ebm_device_images.dependence.images[0].image_uuid
  security_group_ids = [ctyun_security_group.security_group_test.id]
  vpc_id = ctyun_vpc.vpc_test.id
  system_disk_size = 100
  system_disk_type = "sata"
  subnet_id = ctyun_subnet.subnet_test.id
}

# 创建一台本地盘弹性裸金属
resource "ctyun_ebm" "ebm_test2" {
  instance_name = "tf-ebm-for-ebm"
  hostname = "tf-ebm-for-ebm"
  password = var.password
  eip_id = ctyun_eip.eip_test.id
  cycle_type = "on_demand"
  device_type = local.device_type1
  image_uuid = data.ctyun_ebm_device_images.test.images[0].image_uuid
  security_group_ids = [ctyun_security_group.security_group_test.id]
  vpc_id = ctyun_vpc.vpc_test.id
  system_volume_raid_uuid = local.system_raid_id
  data_volume_raid_uuid = local.data_raid_id
  status = "running"
  subnet_id = ctyun_subnet.subnet_test.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cycle_type` (String) 订购周期类型，取值范围:[on_demand=按需,month=按月,year=按年]
- `device_type` (String) 物理机套餐类型，可通过ctyun_ebm_device_types查询
- `hostname` (String) hostname，linux系统2到63位长度；windows系统2-15位长度；<br/>允许使用大小写字母、数字、连字符'-'、点号'.'，不能连续使用'-'或者'.'，'-'和'.'不能用于开头或结尾，不能仅使用数字，支持更新
- `image_uuid` (String) 物理机镜像id，可通过ctyun_ebm_device_images查询
- `instance_name` (String) 物理机名称，长度为2-31位，支持更新
- `subnet_id` (String) 主网卡的子网id
- `vpc_id` (String) 主网卡虚拟私有云ID

### Optional

- `auto_renew` (Boolean) 是否自动续订，默认非自动续订，当cycle_type不等于on_demand时才可填写。
- `az_name` (String) 可用区名称
- `cycle_count` (Number) 订购时长，最长订购周期为60个月（5年）；非按需时必填
- `data_volume_raid_uuid` (String) 本地数据盘raid类型，如果有本地盘则必填，可通过ctyun_ebm_device_raids查询
- `eip_id` (String) 弹性公网IP的ID
- `key_pair_name` (String) 密钥对名词，和password只能传其中之一
- `password` (String, Sensitive) 密码(必须包含大小写字母和（一个数字或者特殊字符）长度8到30位)，未传入有效的keyName时必须传入password，支持更新
- `project_id` (String) 企业项目ID，如果不填则默认使用provider ctyun中的project_id或环境变量中的CTYUN_PROJECT_ID
- `region_id` (String) 资源池ID，如果不填则默认使用provider ctyun中的region_id或环境变量中的CTYUN_REGION_ID
- `security_group_ids` (Set of String) 主网卡安全组ID，套餐smart_nic_exist为true可支持安全组。创建弹性裸金属必须传入安全组ID，标准裸金属不支持传入安全组ID
- `status` (String) 物理机状态，支持running（开机）和stopped（关机），默认running，支持更新
- `system_disk_size` (Number) 系统盘大小，单位为G，取值范围：[100, 2048]，当前不支持公网
- `system_disk_type` (String) 系统盘类型，sata：普通IO，sas：高IO，ssd：超高IO
- `system_volume_raid_uuid` (String) 本地系统盘raid类型，如果有本地盘则必填，可通过ctyun_ebm_device_raids查询
- `user_data` (String) 用户自定义数据，需要以Base64方式编码，Base64编码后的长度限制为1-16384字符

### Read-Only

- `actual_image_id` (String) 实际镜像id，重装、集群纳管等操作会导致actual_image_id与image_id不同
- `eip_address` (String) 弹性公网IP的地址
- `fixed_ip` (String) 加入子网后的ip地址
- `id` (String) ID
- `instance_id` (String) 物理机UUID，值与id相同
- `interface_id` (String) 主网卡UUID
- `master_order_id` (String) 订单id
- `name` (String) 名称
- `port_id` (String) 主网卡PORT UUID
- `system_disk_id` (String) 系统盘的id
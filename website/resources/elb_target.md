# ctyun_elb_target (Resource)
**详细说明请见文档：https://www.ctyun.cn/document/10026756/10196689**



## 样例

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-for-elb"
  cidr        = "192.168.0.0/16"
  description = "terraform测试使用"
  enable_ipv6 = true
}
resource "ctyun_elb_target_group" "target_group_test" {
  name      = "tf-tg-for-target1_12"
  vpc_id    = ctyun_vpc.vpc_test.id
  algorithm = "wrr"
}

data "ctyun_images" "image_test" {
  name       = "CentOS Linux 8.4"
  visibility = "public"
  page_no = 1
  page_size = 10
}

data "ctyun_ecs_flavors" "ecs_flavor_test" {
  cpu    = 2
  ram    = 4
  arch   = "x86"
  series = "C"
  type   = "CPU_C7"
}
resource "ctyun_subnet" "subnet_test" {
  vpc_id = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-for-elb"
  cidr        = "192.168.1.0/24"
  description = "terraform测试使用"
  dns         = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
}



resource "ctyun_ecs" "ecs_test" {
  instance_name       = "tf-ecs-for-elb"
  display_name        = "tf-ecs-for-elb"
  flavor_id           = data.ctyun_ecs_flavors.ecs_flavor_test.flavors[0].id
  image_id            = data.ctyun_images.image_test.images[0].id
  system_disk_type    = "sata"
  system_disk_size    = 40
  vpc_id = ctyun_vpc.vpc_test.id
  password            = var.password
  cycle_type          = "on_demand"
  subnet_id = ctyun_subnet.subnet_test.id
  is_destroy_instance = false
}

variable "password" {
  type      = string
  sensitive = true
}

resource "ctyun_elb_target" "elb_target_test" {
  target_group_id = ctyun_elb_target_group.target_group_test.id
  instance_type = "VM"
  instance_id = ctyun_ecs.ecs_test.id
  protocol_port = 12345
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `instance_id` (String) 云主机或物理机，或弹性容器实例ID
- `instance_type` (String) 实例类型。取值范围：VM-虚拟云主机、BM-物理机、ECI-弹性容器
- `protocol_port` (Number) 协议端口。取值范围：1-65535，支持更新
- `target_group_id` (String) 后端服务组Id

### Optional

- `az_name` (String) 可用区名称，如果不填则默认使用provider ctyun中的project_id或环境变量中的CTYUN_PROJECT_ID
- `description` (String) 描述，支持拉丁字母、中文、数字, 特殊字符：~!@#$%^&*()_-+= <>?:'{},./;'[,]·~！@#￥%……&*（） —— -+={},，支持更新
- `instance_ip` (String) 后端实例ip
- `project_id` (String) 企业项目ID，如果不填则默认使用provider ctyun中的project_id或环境变量中的CTYUN_PROJECT_ID
- `region_id` (String) 资源池Id，默认使用provider ctyun总region_id 或者环境变量
- `weight` (Number) 后端实例权重。取值范围：1-256，默认为100，支持更新

### Read-Only

- `created_time` (String) 创建时间，为UTC格式
- `health_check_status` (String) IPv4的健康检查状态: offline / online / unknown
- `health_check_status_ipv6` (String) IPv6的健康检查状态: offline / online / unknown
- `id` (String) 后端主机服务(elb_target)ID
- `status` (String) 状态: DOWN / ACTIVE
- `updated_time` (String) 更新时间，为UTC格式
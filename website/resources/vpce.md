# ctyun_vpce (Resource)
**详细说明请见文档：https://www.ctyun.cn/document/10042658/10217121**



## 样例

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-test-qqq"
  cidr        = "192.168.0.0/16"
  description = "terraform测试使用"
  enable_ipv6 = true
}

resource "ctyun_subnet" "subnet_test" {
  vpc_id = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-test"
  cidr        = "192.168.1.0/24"
  description = "terraform测试使用"
  dns         = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
  enable_ipv6 = true
}

resource "ctyun_vpce_service" "test" {
  name  = "tf-vpce-server-sss"
  vpc_id = ctyun_vpc.vpc_test.id
  subnet_id = ctyun_subnet.subnet_test.id
  auto_connection = true
  type = "interface"
  instance_id = "d40b78e2-23de-4fa6-baf0-e500750f985b"
  instance_type = "vm"
  rules = [{
    protocol = "TCP"
    endpoint_port = 2
    server_port = 2
  },
  ]
}

resource "ctyun_vpce" "test" {
  name  = "tf-vpce-123"
  endpoint_service_id = ctyun_vpce_service.test.id
  vpc_id = ctyun_vpc.vpc_test.id
  subnet_id = ctyun_subnet.subnet_test.id
  whitelist_flag = true
  whitelist_cidr = ["192.168.1.0/24"]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `endpoint_service_id` (String) 终端节点服务ID
- `name` (String) 支持拉丁字母、中文、数字，下划线，连字符，中文/英文字母开头，不能以http:/https:开头，长度2-32，支持更新
- `subnet_id` (String) 子网ID
- `vpc_id` (String) 虚拟私有云ID
- `whitelist_flag` (Boolean) 是否开启白名单，支持更新

### Optional

- `region_id` (String) 资源池ID，如果不填则默认使用provider ctyun中的region_id或环境变量中的CTYUN_REGION_ID
- `subnet_ip` (String) 子网IP
- `whitelist_cidr` (Set of String) 白名单列表，当whitelist_flag=true是必填，最多同时支持20个地址，最少输入一个，支持更新

### Read-Only

- `id` (String) ID
- `master_order_id` (String) 主订单号
- `status` (Number) endpoint状态, 1 表示已链接，2 表示未链接
# ctyun_scaling_group (Resource)
**详细说明请见文档：：https://www.ctyun.cn/document/10027725**



## 样例

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

variable "key_pair" {
  type      = string
  sensitive = true
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-for-scaling"
  cidr        = "192.168.0.0/16"
  description = "terraform-kafka测试使用"
  enable_ipv6 = true
}

resource "ctyun_subnet" "subnet_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-for-scaling"
  cidr        = "192.168.1.0/24"
  description = "terraform-scaling测试使用"
  dns = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
}
resource "ctyun_security_group" "sg_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-sg-for-scaling"
  description = "terraform-scaling测试使用"
  lifecycle {
    prevent_destroy = false
  }
}

resource "ctyun_elb_loadbalancer" "elb_test" {
  subnet_id     = ctyun_subnet.subnet_test.id
  name          = "tf-elb-for-scaling-group"
  sla_name      = "elb.s2.small"
  resource_type = "internal"
  vpc_id        = ctyun_vpc.vpc_test.id
  cycle_type    = "on_demand"
}

resource "ctyun_elb_health_check" "test" {
  name     = "tf-hc-for-scaling"
  protocol = "TCP"
}

resource "ctyun_elb_target_group" "target_group_test" {
  name      = "tf_target_group"
  vpc_id    = ctyun_vpc.vpc_test.id
  algorithm = "wrr"
  health_check_id = ctyun_elb_health_check.test.id
  session_sticky_mode = "SOURCE_IP"
  source_ip_timeout = 30
  proxy_protocol = 1
  protocol = "TCP"
}

resource "ctyun_elb_listener" "elb_listener_test" {
  loadbalancer_id     = ctyun_elb_loadbalancer.elb_test.id
  name                = "tf_listener_scaling"
  protocol            = "TCP"
  protocol_port       = 12345
  default_action_type = "forward"
  target_groups = [{ target_group_id = ctyun_elb_target_group.target_group_test.id }]
  listener_cps        = 1
  establish_timeout   = 100
}


data "ctyun_images" "image_test" {
  name       = "CentOS Linux 8.4"
  visibility = "public"
  page_no = 1
  page_size = 10
}

locals {
  image_id = data.ctyun_images.image_test.images[0].id
}

resource "ctyun_keypair" "scaling_test" {
  name       = "key-pair-scaling-test"
  public_key = var.key_pair
}

resource "ctyun_scaling_config" "config_test" {
  name            = "sc-for-policy"
  image_id        =  local.image_id
  flavor_name     = "s7.large.2"
  use_floatings   = "diable"
  login_mode      = "key_pair"
  key_pair_id     = ctyun_keypair.scaling_test.id
  monitor_service = true
  az_names        = ["cn-huadong1-jsnj1A-public-ctcloud"]
  volumes         = [{"volume_type":"SATA", "volume_size": 40, "flag":"OS"}]
}

resource "ctyun_scaling_group" "scaling_group_test" {
  security_group_id_list = [ctyun_security_group.sg_test.id]
  name                   = "scaling-group-example"
  health_mode            = "server"
  subnet_id_list         = [ctyun_subnet.subnet_test.id]
  move_out_strategy      = "earlier_config"
  vpc_id                 = ctyun_vpc.vpc_test.id
  min_count              = 1
  max_count              = 50
  expected_count         = 1
  health_period          = 300
  use_lb                 = 1
  lb_list                = [ctyun_elb_loadbalancer.elb_test.id]
  config_list            = [ctyun_scaling_config.config_test.id]
  az_strategy            = "priority_distribution"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `az_strategy` (String) 扩容策略类型：uniform_distribution-均衡分布，priority_distribution-优先级分布。支持更新
- `health_mode` (String) 健康检查方式：server-云服务器健康检查，lb-弹性负载均衡健康检查。支持更新，当选择lb（弹性负载均衡健康检查）时，use_lb=1
- `health_period` (Number) 健康检查时间间隔（周期），单位：秒，取值范围：[300,10080]。支持更新
- `max_count` (Number) 最大云主机数，取值范围：[min_count,2147483647]。支持更新
- `min_count` (Number) 最小云主机数，取值范围：[0,50]。支持更新
- `move_out_strategy` (String) 实例移出策略：earlier_config-较早创建的配置较早创建，later_config-较晚创建的配置较晚创建，earlier_vm-较早创建的云主机，later_vm-较晚创建的云主机。支持更新
- `name` (String) 伸缩组名称。支持更新。
- `subnet_id_list` (Set of String) 子网ID列表，支持一主多辅。最多支持5个。支持更新，但是更新阶段status必须为2（停用）
- `use_lb` (Number) 是否使用负载均衡：1-是，2-否。status=disable时支持更新
- `vpc_id` (String) 虚拟私有云ID

### Optional

- `add_instance_uuid_list` (Set of String) 需要手动添加的云主机uuid列表。伸缩组内云主机清单可以根据data.ctyun_scaling_ecs_list获取。支持更新。
- `config_list` (Set of Number) 伸缩配置ID列表，最大支持传入10个伸缩配置。支持更新
- `delete_protection` (String) 控制伸缩组保护，开启伸缩组保护，不可删除该伸缩组。取值范围：enable 或 disable，支持更新。可以用于控制伸缩组保护的开启/关闭
- `expected_count` (Number) 期望云主机数，非多可用区资源池不支持该参数。支持更新，若未填写。expected_count默认=min_count
- `is_destroy` (Boolean) 移除时是否销毁，仅当移除云主机时生效（对手动添加的机器做处理），true-ecs移出伸缩组时销毁， false-ecs移出伸缩组时不销毁，支持更新
- `lb_list` (Attributes List) 负载均衡列表，use_lb=1时必填。当status=disable时支持更新 (see [below for nested schema](#nestedatt--lb_list))
- `project_id` (String) 企业项目ID，如果不填则默认使用provider ctyun中的project_id或环境变量中的CTYUN_PROJECT_ID
- `region_id` (String) 资源池ID
- `remove_instance_uuid_list` (Set of String) 需要删除手动/自动加入伸缩组的云主机uuid列表。伸缩组内云主机清单可以根据data.ctyun_scaling_ecs_list获取。支持更新。
- `security_group_id_list` (Set of String) 安全组ID列表。支持更新
- `status` (String) 伸缩组状态。取值范围：enable 或 disable，支持更新。可以用于控制伸缩组的状态更新

### Read-Only

- `id` (Number) 伸缩组ID
- `real_count` (Number) 当前的云主机数量，直接通过接口获取，一般为expected_count + 手动添加（+）/移除（-）云主机数量。

<a id="nestedatt--lb_list"></a>
### Nested Schema for `lb_list`

Required:

- `host_group_id` (String) 后端主机组ID，当status=disable时支持更新
- `lb_id` (String) 负载均衡ID，当status=disable时支持更新
- `port` (Number) 端口号，当status=disable时支持更新
- `weight` (Number) 权重，当status=disable时支持更新
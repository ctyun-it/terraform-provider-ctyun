# ctyun_postgresql_association_eip (Resource)
-> 详细说明请见文档：https://www.ctyun.cn/document/10034019/10174601



## Example

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-for-pgsql"
  cidr        = "192.168.0.0/16"
  description = "terraform-kafka测试使用"
  enable_ipv6 = true
}

resource "ctyun_subnet" "subnet_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-for-pgsql"
  cidr        = "192.168.1.0/24"
  description = "terraform-kafka测试使用"
  dns = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
}
resource "ctyun_security_group" "sg_pgsql_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-sg-for-esc"
  description = "terraform-kafka测试使用"
  lifecycle {
    prevent_destroy = false
  }
}

variable "password" {
  type      = string
  sensitive = true
}

resource "ctyun_postgresql_instance" "test" {
  cycle_type            = "on_demand"
  host_type             = "S7"
  prod_id               = "Single1222"
  storage_type          = "SATA"
  storage_space         = 100
  name                  = "pgsql-test"
  password              = var.password
  case_sensitive        = true
  instance_series       = "S"
  prod_performance_spec = "2C4G"
  vpc_id                = ctyun_vpc.vpc_test.id
  subnet_id             = ctyun_subnet.subnet_test.id
  security_group_id     = ctyun_security_group.sg_pgsql_test.id
  availability_zone_info = [
    { "availability_zone_name" : "cn-gs-qyi2-1a-public-ctcloud", "availability_zone_count" : 1, "node_type" : "master" }
  ] // availability_zone_name值根据情况而定
  backup_storage_type   = "SATA"
  backup_storage_space  = 100
  os_type               = "ctyunos"
  cpu_type              = "Intel"
}

resource "ctyun_eip" "eip_test" {
  name                = "tf-eip-for-pgsql"
  bandwidth           = 1
  cycle_type          = "on_demand"
  demand_billing_type = "upflowc"
}

resource "ctyun_postgresql_association_eip" "pgsql_association_eip_test" {
  eip_id = ctyun_eip.eip_test.id
  eip    = ctyun_eip.eip_test.address
  inst_id = ctyun_postgresql_instance.test.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `eip_id` (String) 弹性id
- `inst_id` (String) 实例id

### Optional

- `project_id` (String) 企业项目ID，如果不填则默认使用provider ctyun中的project_id或环境变量中的CTYUN_PROJECT_ID
- `region_id` (String) 资源池id,如果不填这默认使用provider ctyun总region_id 或者环境变量

### Read-Only

- `eip` (String) 弹性ip地址
- `eip_status` (Number) 弹性ip状态 0->unbind，1->bind,2->binding
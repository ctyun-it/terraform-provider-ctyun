# ctyun_mysql_association_eip (Resource)
-> 详细说明请见文档：https://www.ctyun.cn/document/10033813/10033927



## Example

```terraform
terraform {
  required_providers {
    ctyun = {
      source = "ctyun-it/ctyun"
    }
  }
}

# 可参考index.md，在环境变量中配置ak、sk、资源池ID、可用区名称
provider "ctyun" {
  env = "prod"
}

resource "ctyun_vpc" "vpc_test" {
  name        = "tf-vpc-for-mysql"
  cidr        = "192.168.0.0/16"
  description = "terraform-kafka测试使用"
  enable_ipv6 = true
}

resource "ctyun_subnet" "subnet_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-subnet-for-mysql1"
  cidr        = "192.168.1.0/24"
  description = "terraform-kafka测试使用"
  dns = [
    "114.114.114.114",
    "8.8.8.8",
    "8.8.4.4"
  ]
}
resource "ctyun_security_group" "sg_mysql_test" {
  vpc_id      = ctyun_vpc.vpc_test.id
  name        = "tf-sg-for-esc"
  description = "terraform-kafka测试使用"
  lifecycle {
    prevent_destroy = false
  }
}

resource "ctyun_mysql_instance" "mysql_test" {
  cycle_type            = "on_demand"
  vpc_id                = ctyun_vpc.vpc_test.id
  subnet_id             = ctyun_subnet.subnet_test.id
  security_group_id     = ctyun_security_group.sg_mysql_test.id
  name                  = "mysql-test-web-2"
  prod_id               = "Master2Slave80"
  storage_type          = "SATA"
  storage_space         = 100
  password              = var.password
  flavor_name           = "c7.2xlarge.4"
}

variable "password" {
  type      = string
  sensitive = true
}

resource "ctyun_eip" "eip_test" {
  name                = "tf-eip-for-mysql"
  bandwidth           = 1
  cycle_type          = "on_demand"
  demand_billing_type = "upflowc"
}

resource "ctyun_mysql_association_eip" "association_eip" {
  eip_id = ctyun_eip.eip_test.id
  inst_id = ctyun_mysql_instance.mysql_test.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `eip_id` (String) 弹性IP的id
- `inst_id` (String) 实例id

### Optional

- `project_id` (String) 企业项目id
- `region_id` (String) 资源池Id

### Read-Only

- `eip_status` (Number) 弹性ip状态 0->unbind，1->bind,2->binding
- `status` (String) eip绑定状态，与eip_status一致